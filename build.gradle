plugins {
    id "java"
    id "application"
    id "com.github.johnrengelman.shadow" version "1.2.4"
    id "org.sonarqube" version "2.5"
}

repositories {
    jcenter()
}

sourceCompatibility = project.property("sourceVersion")

group = "com.github.pop4959.srbot"
mainClassName = "com.github.pop4959.srbot.Main"
version = project.property("version")
jdaVersion = project.property("jdaVersion")
botFolder = project.property("botFolder")

compileJava {
    options.encoding = project.property("compileJavaEncoding")
    options.compilerArgs += ["-Xlint:deprecation"]
}

dependencies {
    compile "net.dv8tion:JDA:$jdaVersion"
    compile "com.ibasco.agql:agql-steam-webapi:0.1.4"
    compile "org.slf4j:slf4j-api:1.7.26"
    compile "org.slf4j:slf4j-simple:1.7.26"
    compile "org.reflections:reflections:0.9.11"
    compile "org.glassfish.jersey.core:jersey-common:2.28"
    compile "org.glassfish.jersey.containers:jersey-container-servlet:2.28"
    compile "org.glassfish.jersey.containers:jersey-container-grizzly2-http:2.28"
    compile "org.glassfish.jersey.inject:jersey-hk2:2.28"
    compile "com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:2.9.8"
    compile "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.9.8"
}

task cleanBotJar(type: Delete) {
    delete fileTree(dir: "${botFolder}", include: "*.jar")
}

task copyBotJar(dependsOn: "shadowJar", type: Copy) {
    from("${buildDir}/libs") {
        include "SRBot-${version}-all.jar"
    }
    rename("-all", "")
    into botFolder
}

task prepareBot(dependsOn: ["cleanBotJar", "copyBotJar"]) {}

task startBot(dependsOn: "prepareBot", type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = mainClassName
    workingDir = botFolder
}
